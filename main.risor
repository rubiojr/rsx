import cli
import rsx
import exec
import os
import filepath

version := "0.1.0"
debug := false

func cleanup(dir) {
  rsx.debug('Cleaning up {dir}')
  if debug {
    rsx.debug('Debug enabled, not removing temp dir {dir}')
  }
  os.remove_all(dir)
}

func go_build(dir, output) {
  cd(dir)

  rsx.debug('Running go mod init {output}')
  exec("go", ["mod", "init", output])

  rsx.debug('Running go mod tidy')
  exec("go", ["mod", "tidy"])

  rsx.debug('Running go build -o {output}...')
  exec("go", ["build", "-o", filepath.join(os.getwd(), output)])
}

func copy_libs(dir) {
  filepath.walk_dir("lib", func(path, dir_entry, err) {
    if dir_entry.type == "regular" {
      dest := filepath.join(dir, path)
      rsx.debug('Copying file {path} to {dest}')
      cp(path, dest)
    }
  })
}

func build(output) {
  tmpdir := os.mkdir_temp("/tmp", "rsx")
  rsx.debug('Building in {tmpdir}...')

  copy := {
    "/main.go": _mainGo,
    "/importer.go": _importerGo,
    "/main.risor": os.read_file("main.risor"),
    "/lib/rsx.risor": rsxLib,
  }

  os.mkdir_all(filepath.join(tmpdir, "/lib"))

  for k, v := range copy {
    os.write_file(filepath.join(tmpdir, k), v)
  }

  copy_libs(tmpdir)
  go_build(tmpdir, output)

  cleanup(tmpdir)
}

cli.app({
    name: "rsx",
    usage_string: "Build risor enabled binaries",
    usage: "build Risor enabled binaries",
    version: version,
    commands: [
        cli.command({
            args: true,
            name: "new",
            description: "Build the current directory",
            action: func(ctx) {
              name := ctx.args()[0]
              os.mkdir(name)
              os.write_file(filepath.join(name, "main.risor"), "import rsx\n"+`rsx.log("Hello, World!")`)
              os.mkdir(filepath.join(name, "lib"))
              os.write_file(filepath.join(name, "/lib/rsx.risor"), rsxLib)
            },
            flags: [
                cli.flag({
                    name: "debug",
                    type: "bool",
                }),
            ],
        }),
        cli.command({
            args: true,
            name: "build",
            description: "Build the current directory",
            action: func(ctx) {
              if ctx.bool("debug") {
                debug = true
                os.setenv("RSX_DEBUG", "1")
              }
              output := ctx.string("output")
              build(output)
            },
            flags: [
                cli.flag({
                    name: "output",
                    aliases: ["o"],
                    type: "string",
                    value: filepath.split(os.getwd())[-1],
                }),
                cli.flag({
                    name: "debug",
                    type: "bool",
                }),
            ],
        }),
    ],
}).run()
