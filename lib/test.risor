import color
import exec
import regexp
import os
import uuid

__fatal := false
__raises := false

_red := color.color(color.fg_red, color.bold)
_green := color.color(color.fg_green, color.bold)

failed_is_fatal := func(b) {
  __fatal = b
}

_last_error := nil
// internal use only, to self-test this library
failed_raises := func() {
  __raises = true
}
// internal use only, to self-test this library
failed_does_not_raise := func() {
  __raises = false
}

_raised_error := errors.new("error raised")

func exit_if_fatal() {
  if __fatal {
    os.exit(1)
  }
}

// Print a message in green.
//
// desc: the test description
func print_ok(desc) {
  if desc != "" {
    print(_green.sprintf('%s:', "  ok"), desc)
  }
}

// Print a message in red.
//
// desc: the test description
func print_fail(desc) {
  if __raises {
    error(desc)
  }
  print(_red.sprintf('%s:', "fail"), desc)
  exit_if_fatal()
}

// Assert that a condition is true.
//
// desc: the test description
// cond: the condition to check (boolean)
func is_true(desc, cond){
  if !cond {
    print_fail(desc)
  } else {
    print_ok(desc)
  }
}

// Assert that a condition is false.
//
// desc: the test description
// cond: the condition to check (boolean)
func is_false(desc, cond){
  if cond {
    print_fail(desc)
  } else {
    print_ok(desc)
  }
}

func equals(desc, o1, o2) {
  if o1 == o2 {
    print_ok(desc)
  } else {
    print_fail(desc + ' (expected: {o1}, got: {o2})')
  }
}

// Assert a condition rasies an error.
//
// desc: the test description
// fn: the function that should raise an error
// err: the error that should be raised
func raises(desc, fn, err="") {
  processed := false
  o := try(fn, func(e) {
    processed = true
    if e != err{
      print_fail(desc + '(expected: {err}, got: {e})')
    } else {
      print_ok(desc)
    }
  }, func(e) {
    print_ok(desc)
  })

  if o == nil && !processed {
    print(sprintf("%s %s", _red.sprintf('%s:', "fail"), desc))
  }
}

// Assert a condition does not raise an error.
//
// desc: the test description
// fn: the function that should not raise an error
func nothing_raised(desc, fn) {
  happened := nil
  try(fn, func(e) {
    happened = e
  })
  if happened {
    error(sprintf("%s %s", _red.sprintf('%s:', "fail"), desc))
  } else {
    print_ok(desc)
  }
}

// Assert that cmd output equals a expected string.
//
// desc: the test description
// cmd: the command to run
// expected: the expected output
func stdout_equals(desc, cmd, expected) {
  tokens := cmd.split(" ")
  bin := tokens[0]
  args := []
  if len(tokens) > 1 {
    args.extend(tokens[1:])
  }

  try(func() {
    out := exec(bin, args)
    if out.stdout != expected {
      errors.new(desc + ' (expected: {expected}, got: {out})')
    } else {
      print_ok(desc)
    }
  }, func(e) {
    print_fail(desc + ' (error: {e})')
  })

}

// Assert that cmd output matches a regular expression.
//
// desc: the test description
// cmd: the command to run
// reg: the regular expression to match
func stdout_matches(desc, cmd, reg) {
  tokens := cmd.split(" ")
  bin := tokens[0]
  args := [bin]
  if len(tokens) > 1 {
    args.extend(tokens[1:])
  }

  cmd := exec.command(args)
  try(func() {
    out := cmd.combined_output()
    out = string(out)
    if regexp.match(reg, out) {
      print_ok(desc)
    } else {
      error(desc + ' (output {out} does not match {reg})')
    }
  }, func(e) {
    print_fail(desc + ' (error: {e})')
  })
}

// Assert that a path is a directory.
//
// desc: the test description
// path: the path to check
func is_dir(desc, path) {
  ok := try(func() { f := os.stat(path); f.is_dir }, false)
  if ok {
    print_ok(desc)
  } else {
    print_fail(desc)
  }
}

// Assert that a path is a file.
//
// desc: the test description
// path: the path to check
func is_file(desc, path) {
  ok := try(func() { f := os.stat(path); f.mode.is_regular }, false)
  if ok {
    print_ok(desc)
  } else {
    print_fail(desc)
    //error(raised_error)
  }
}

// Create a temporary directory and run a function with it.
// The directory created is automatically removed after the function is run.
//
// fn: the function to run
func with_temp_dir(fn) {
  tmp_dir := filepath.join(os.temp_dir(), uuid())
  os.mkdir_all(tmp_dir)
  defer os.remove_all(tmp_dir)
  return fn(tmp_dir)
}
